plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
    id "org.sonarqube" version "3.3"
}

group 'org.ostis'
version '0.6.1'

sourceCompatibility = 17
targetCompatibility = 17

repositories {
    mavenCentral()
}

dependencies {
//  https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.0'

    implementation 'org.java-websocket:Java-WebSocket:1.5.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}

compileJava {
    options.compilerArgs += ['--add-reads', 'JMantic.main=ALL-UNNAMED']
//    options.compilerArgs += ['-Xlint:unchecked'] // use to see more information about compiler warnings
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/artrayme/jmantic")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId 'org.ostis'
            artifactId 'jmantic'
            version '0.6.1'
            from(components.java)
        }
    }
}


// Create jar archive with dependencies
jar {
    from {
        configurations.runtimeClasspath.filter { it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

jacocoTestReport {
    dependsOn test

    reports {
        csv.enabled true
        xml.required = true
        csv.required = true
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }

}

sonarqube {
    properties {
        property "sonar.projectKey", "artrayme_JMantic"
        property "sonar.organization", "artrayme"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}
